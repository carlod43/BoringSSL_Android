import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.application'
}

android {
    compileSdk = 35
    buildToolsVersion = '35.0.0'
    ndkVersion = '27.0.11902837'
    namespace = 'io.github.vvb2060.ndk.boringssl.test'
    defaultConfig {
        minSdk = 21
        targetSdk = 35
        versionCode = 6
        versionName = 'android-14.0.0_r54'
        testInstrumentationRunner = 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            ndkBuild {
                arguments += [ "-j${Runtime.runtime.availableProcessors()}" ]
                abiFilters += [ "x86", "x86_64", "armeabi-v7a", "arm64-v8a", "riscv64" ]
            }
        }
        ndk {
            debugSymbolLevel = "FULL"
        }
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            signingConfig = signingConfigs.debug
            proguardFiles("proguard-rules.pro")
            externalNativeBuild {
                ndkBuild {
                    arguments += [ "enableLTO=1" ]
                }
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path file('../boringssl/src/main/native/Android.mk')
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    buildFeatures {
        buildConfig = false
        prefab = true
    }

    lint {
        checkReleaseBuilds = false
    }

    dependenciesInfo {
        includeInApk = false
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = false
        }
    }

    testOptions {
        devices {
            api34(ManagedVirtualDevice) {
                device = "Pixel 2"
                apiLevel = 34
                systemImageSource = "aosp-atd"
                require64Bit = true
            }
            api30(ManagedVirtualDevice) {
                device = "Pixel 2"
                apiLevel = 30
                systemImageSource = "aosp-atd"
                require64Bit = true
            }
        }
    }
}

dependencies {
//    implementation 'io.github.vvb2060.ndk:boringssl:4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'
}

import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 32
    buildToolsVersion '32.0.0'
    ndkVersion '23.1.7779620'
    defaultConfig {
        applicationId 'io.github.vvb2060.boringssl.test'
        minSdkVersion 16
        targetSdkVersion 32
        versionCode 3
        versionName 'android-t-preview-2'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            ndkBuild.arguments "-j${Runtime.runtime.availableProcessors()}"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug
        }
    }

    externalNativeBuild {
        ndkBuild {
            path file('../boringssl/src/main/native/Android.mk')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        buildConfig false
        prefab true
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging false
        }
    }

    testOptions {
        devices {
            atd64bit(ManagedVirtualDevice) {
                device = "Pixel 2"
                apiLevel = 31
                systemImageSource = "aosp-atd"
                require64Bit = true
            }
            atd32bit(ManagedVirtualDevice) {
                device = "Pixel 2"
                apiLevel = 30
                systemImageSource = "aosp-atd"
                require64Bit = false
            }
        }
    }
}

dependencies {
//    implementation 'io.github.vvb2060.ndk:boringssl:3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:rules:1.4.0'
}

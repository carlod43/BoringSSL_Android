import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 34
    buildToolsVersion '34.0.0'
    ndkVersion '26.1.10909125'
    namespace 'io.github.vvb2060.ndk.boringssl.test'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 5
        versionName 'android-14.0.0_r18'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            ndkBuild.arguments "-j${Runtime.runtime.availableProcessors()}"
        }
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            signingConfig = signingConfigs.debug
            proguardFiles("proguard-rules.pro")
        }
    }

    externalNativeBuild {
        ndkBuild {
            path file('../boringssl/src/main/native/Android.mk')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig false
        prefab true
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging false
        }
    }

    testOptions {
        devices {
            atd64bit(ManagedVirtualDevice) {
                device = "Pixel 2"
                apiLevel = 31
                systemImageSource = "aosp-atd"
                require64Bit = true
            }
            atd32bit(ManagedVirtualDevice) {
                device = "Pixel 2"
                apiLevel = 30
                systemImageSource = "aosp-atd"
                require64Bit = false
            }
        }
    }
}

dependencies {
//    implementation 'io.github.vvb2060.ndk:boringssl:4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
}
